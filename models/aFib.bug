#This code is adapted from
#Dias, S., Welton, N.J., Sutton, A.J. & Ades, A.E. NICE DSU Technical Support Document 2: A Generalised Linear Modelling Framework for Pairwise and Network Meta-Analysis of Randomised Controlled Trials. 2011; last updated September 2016 (available from http://www.nicedsu.org.uk).


# Binomial likelihood, log link
# Random effects model for multi-arm trials

model{                               # *** PROGRAM STARTS
  for(i in 1:ns){                      # LOOP THROUGH STUDIES
    w[i,1] <- 0    # adjustment for multi-arm trials is zero for control arm
    delta[i,1] <- 0             # treatment effect is zero for control arm
    #mu[i] ~ dnorm(0,.0001)           # vague priors for all trial baselines
    mu[i] <- log(p.base[i])
	  p.base[i] ~ dunif(0, 1)
    for (k in 1:na[i]) {             # LOOP THROUGH ARMS
        r[i,k] ~ dbin(p[i,k],n[i,k]) # Binomial likelihood
        # model for linear predictor
        log(p[i,k]) <- min(mu[i] + delta[i,k], -1E-16)
        rhat[i,k] <- p[i,k] * n[i,k] # expected value of the numerators 
    }
    for (k in 2:na[i]) {             # LOOP THROUGH ARMS
      # trial-specific LOR distributions
      delta[i,k] ~ dnorm(md[i,k],taud[i,k])
      # mean of LOR distributions, with multi-arm trial correction
      md[i,k] <-  d[t[i,k]] - d[t[i,1]] + sw[i,k]
      # precision of LOR distributions (with multi-arm trial correction)
      taud[i,k] <- tau *2*(k-1)/k
      # adjustment, multi-arm RCTs
      w[i,k] <- (delta[i,k] - d[t[i,k]] + d[t[i,1]])
      # cumulative adjustment for multi-arm trials
      sw[i,k] <- sum(w[i,1:(k-1)])/(k-1)
    }
  }   
  
  d[1]<-0       # treatment effect is zero for reference treatment

  # vague priors for treatment effects
  for (k in 2:nt){  
    d[k] ~ dnorm(0,(15*2.51)^(-2)) 
  }

  sd ~ dunif(0,2.51)     # vague prior for between-trial SD
  tau <- pow(sd,-2)   # between-trial precision = (1/between-trial variance)

}
